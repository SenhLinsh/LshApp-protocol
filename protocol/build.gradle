if (isRunAlone.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (isRunAlone.toBoolean()) {
                manifest.srcFile 'src/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    /////// 以下为配置 library 注释在打包 jar 后保留 /////////
    // 打包源码 jar
    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }
    task javadoc(type: Javadoc) {
        failOnError false
        source = android.sourceSets.main.java.sourceFiles
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += configurations.compile
    }
    // 打包文档 jar
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    if (isRunAlone.toBoolean()) {
        implementation 'com.android.support.constraint:constraint-layout:1.1.3'
        implementation 'com.android.support:appcompat-v7:27.1.1'
        implementation 'com.android.support:design:27.1.1'

        implementation 'io.reactivex.rxjava2:rxjava:2.2.0'
        implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    } else {
        compileOnly 'com.android.support.constraint:constraint-layout:1.1.3'
        compileOnly 'com.android.support:appcompat-v7:27.1.1'
        compileOnly 'com.android.support:design:27.1.1'

        compileOnly 'io.reactivex.rxjava2:rxjava:2.2.0'
        compileOnly 'com.squareup.okhttp3:okhttp:3.11.0'
    }
}
